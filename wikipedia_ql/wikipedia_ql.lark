query: "from"i _S* source _S* nested_selectors

source: ( _source_type ":" )? STRING

_source_type: IDENT

// TODO: Selector-sequence: foo >> bar, foo > bar

nested_selectors: ("{" _SM* selectors _SM* "}" | _SM* ">>" _SM* selector | _SM* follow_link)

?selectors: selector _S* (";" _SM* selector)* ";" ?
selector: _selector_expression attribute_selector? as_named? _S* nested_selectors?

follow_link: "->" _SM* nested_selectors

// TODO: and/or type
as_named: _S+ "as"i _S+ STRING

_selector_expression: page_selector
                      | text_selector
                      | sentence_selector
                      | section_selector
                      | text_group_selector
                      | css_selector
                      | attribute_selector

page_selector: "page"
text_selector: "text" ( "[" _S* STRING _S* "]" )?
sentence_selector: "sentence" ("[" _S* STRING _S* "]")?

section_selector: "section" ( attrib )*
text_group_selector: "text-group" "[" _S* ( STRING | NUMBER ) _S* "]"

attribute_selector: "@" IDENT

// https://www.w3.org/TR/CSS21/grammar.html
css_selector: element_name (HASH | class | attrib | pseudo)* | (HASH | class | attrib | pseudo)+
element_name: IDENT | "*"
class: "." IDENT
attrib: "[" _S* IDENT _S* ( ATTR_OP _S* ( IDENT | STRING ) _S* )? "]"
// pseudo: ":" IDENT ("(" _S* [IDENT _S*]? ")")?
pseudo: ":" IDENT ("(" _S* (IDENT | NUMBER ) _S* ")")?

ATTR_OP: "=" | INCLUDES | DASHMATCH

FUNCTION: IDENT "("
INCLUDES: "~="
DASHMATCH: "|="
HASH: "#" NAME
IDENT:   "-"? NMSTART NMCHAR*
NAME:    NMCHAR+
NONASCII:  /[\240-\377]/
UNICODE:   /\\[0-9a-f]{1,6}(\r\n|[ \t\r\n\f])?/
ESCAPE:    UNICODE | /\\[^\r\n\f0-9a-f]/
NMSTART:   /[_a-z]/ | NONASCII | ESCAPE
NMCHAR:    /[_a-z0-9-]/ | NONASCII | ESCAPE

NUMBER: /[0-9]+/

_S: (" "|/\t/)+
_SM: /[ \t\f\r\n]/+

%import common.ESCAPED_STRING -> STRING
%import common.INT

// FIXME: Doesn't work somehow :(
%ignore _S
%ignore _SM
